buildscript {
    ext {
        springBootVersion = "2.0.0.M7"
        guavaVersion = "23.5-jre"
        commonsLangVersion = "3.7"

        junitJupiterVersion = "5.0.2"
        junitPluginVersion = "1.0.2"
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.junit.platform:junit-platform-gradle-plugin:${junitPluginVersion}"
    }
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.junit.platform.gradle.plugin"

group = "info.matsumana.tsujun"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = targetCompatibility = 9
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-webflux")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("com.google.guava:guava:${guavaVersion}")
    compile("org.apache.commons:commons-lang3:${commonsLangVersion}")

    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
    }
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testCompile("io.projectreactor:reactor-test")
}

task yarnBuildProd(type: Exec) {
    commandLine "./yarnBuildProd.sh"
}

bootJar.dependsOn yarnBuildProd
